buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath "org.springframework.cloud:spring-cloud-contract-gradle-plugin:$springCloudContractVersion"
        classpath "io.rest-assured:rest-assured:$restAssuredVersion"
        classpath "io.rest-assured:spring-mock-mvc:$restAssuredVersion"
        classpath "org.asciidoctor:asciidoctor-gradle-plugin:1.5.3"
    }
}

plugins {
    id "com.gorylenko.gradle-git-properties" version "1.4.17"
    id "org.springframework.boot" version "2.0.5.RELEASE"
    id "org.jetbrains.kotlin.jvm" version "1.2.70"
    id "org.jetbrains.kotlin.plugin.allopen" version "1.2.70"
    id "org.jetbrains.kotlin.plugin.spring" version "1.2.70"
    id "org.jetbrains.kotlin.plugin.noarg" version "1.2.70"
}

repositories {
    mavenCentral()
    mavenLocal()
}

configure(subprojects - project(':ci')) {
    repositories {
        mavenCentral()
        mavenLocal()
    }

    group = "com.cloud"
    version = "1.0.0"

    apply plugin: "kotlin"
    apply plugin: "maven-publish"
    apply plugin: "io.spring.dependency-management"

    dependencies {
        compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"
        compile "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"

        runtime "javax.xml.bind:jaxb-api:2.3.0"
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
            mavenBom "org.springframework.cloud:spring-cloud-contract-dependencies:${springCloudContractVersion}"
        }

        dependencies {
            dependency "org.springframework.boot:spring-boot-starter-data-jpa:$springBootVersion"
            dependency "org.springframework.boot:spring-boot-starter-data-rest:$springBootVersion"
            dependency "org.springframework.boot:spring-boot-starter-integration:$springBootVersion"
            dependency "org.springframework.restdocs:spring-restdocs-mockmvc:2.0.2.RELEASE"
            dependency "com.h2database:h2:$h2Version"
        }
    }

    compileKotlin { kotlinOptions { jvmTarget = "1.8" } }
    compileTestKotlin { kotlinOptions { jvmTarget = "1.8" } }
}

task publishContractStubs {
    description "Publishes the Spring Cloud Contract Test stubs to local maven repo"
    dependsOn subprojects.find { p -> p.name == 'reservation-service' }.publishToMavenLocal
}