buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-allopen:$kotlinVersion"
        classpath "org.jetbrains.kotlin:kotlin-noarg:$kotlinVersion"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "org.springframework.cloud:spring-cloud-contract-gradle-plugin:$springCloudContractVersion"
        classpath "io.rest-assured:rest-assured:$restAssuredVersion"
        classpath "io.rest-assured:spring-mock-mvc:$restAssuredVersion"
        classpath "org.asciidoctor:asciidoctor-gradle-plugin:1.5.3"
    }
}

plugins {
    id "com.gorylenko.gradle-git-properties" version "1.4.17"
    id 'org.springframework.boot' version '2.0.5.RELEASE'
}

configure(subprojects - project(':ci')) {
    repositories {
        mavenCentral()
        mavenLocal()
    }

    group = "com.cloud"
    version = "1.0.0"

    apply plugin: "kotlin-spring"
    apply plugin: "kotlin-jpa"
    apply plugin: "maven-publish"
    apply plugin: "io.spring.dependency-management"

    dependencies {
        compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"
        compile "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"

        compile "javax.xml.bind:jaxb-api:2.3.0"
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
            mavenBom "org.springframework.cloud:spring-cloud-contract-dependencies:${springCloudContractVersion}"
        }

        dependencies {
            dependency "org.springframework.boot:spring-boot-starter-data-jpa:$springBootVersion"
            dependency "com.h2database:h2:$h2Version"
        }
    }

    compileKotlin { kotlinOptions { jvmTarget = "1.8" } }
    compileTestKotlin { kotlinOptions { jvmTarget = "1.8" } }
}

task publishContractStubs {
    description "Publishes the Spring Cloud Contract Test stubs to local maven repo"
    dependsOn subprojects.find { p -> p.name == 'reservation-service' }.publishToMavenLocal
}