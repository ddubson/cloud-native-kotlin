buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath "org.springframework.cloud:spring-cloud-contract-gradle-plugin:$springCloudContractVersion"
        classpath "io.rest-assured:rest-assured:$restAssuredVersion"
        classpath "io.rest-assured:spring-mock-mvc:$restAssuredVersion"
    }
}

plugins {
    id "org.springframework.boot" version "1.5.9.RELEASE"
    id "org.jetbrains.kotlin.jvm" version "1.3.21"
    id "org.jetbrains.kotlin.plugin.allopen" version "1.3.21"
    id "org.jetbrains.kotlin.plugin.noarg" version "1.3.21"
    id "com.gorylenko.gradle-git-properties" version "1.4.17"
    id "org.asciidoctor.convert" version "1.5.3"
}

configure(subprojects - project(':ci')) {
    apply plugin: "java"
    apply plugin: "kotlin"
    apply plugin: "kotlin-spring"
    apply plugin: "kotlin-jpa"
    apply plugin: "org.springframework.boot"
    apply plugin: "maven-publish"

    repositories {
        mavenCentral()
        mavenLocal()
    }

    group = "com.cloud"
    version = "1.0.0"

    dependencies {
        implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
        implementation "org.jetbrains.kotlin:kotlin-reflect"
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
            mavenBom "org.springframework.cloud:spring-cloud-contract-dependencies:${springCloudContractVersion}"
        }
    }

    compileKotlin { kotlinOptions { jvmTarget = "1.8" } }
    compileTestKotlin { kotlinOptions { jvmTarget = "1.8" } }
}

task publishContractStubs {
    description "Publishes the Spring Cloud Contract Test stubs to local maven repo"
    dependsOn subprojects.find { p -> p.name == 'reservation-service' }.publishToMavenLocal
}